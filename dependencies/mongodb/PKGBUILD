#
# CCR Packages for Chakra, part of chakra-project.org
#
# maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=mongodb
pkgver=2.6.5
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database.'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('boost-libs' 'pcre' 'openssl')
makedepends=('scons' 'boost' 'readline' 'ncurses' 'libpcap' 'clang') # 'cyrus-sasl')
# checkdepends=('python2-pymongo')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/mongodb.conf')
install="mongodb.install"
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        'mongodb.conf'
        'mongodb.service'
       )

md5sums=('a6ba36e84c291a3e174c36138a470fd1'
         '4839fe1d638187ca3226e8267b947318'
         '96ab4517b48974ce0e566d9746a75a4f')

prepare() {
  cd mongodb-src-r${pkgver}

  # Add missing include required for std::swap()
  # https://github.com/mongodb/mongo/commit/2d5d662
  if grep -q '#include <algorithm>' src/mongo/shell/linenoise_utf8.h; then
    error '<algorithm> include has been fixed upstream; remove this local fix.'
    return 1
  fi
  sed -i '1i #include <algorithm>' src/mongo/shell/linenoise_utf8.h
}

build() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons all \
    --use-system-boost \
    --use-system-pcre \
    --ssl --cc=clang --cxx=clang++ \
    --disable-warnings-as-errors
    # --use-sasl-client
    # --use-system-v8
}

# check() {
#   # Tests not passing
#   export SCONSFLAGS="$MAKEFLAGS"
# 
#   cd mongodb-src-r${pkgver}
# 
#   scons smokeAll \
#     --use-system-boost \
#     --use-system-pcre \
#     --ssl --cc=clang --cxx=clang++ \
#     --smokedbprefix="$srcdir" || :
# }

package() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons install \
    --use-system-boost \
    --use-system-pcre \
    --ssl --cc=clang --cxx=clang++ \
    --disable-warnings-as-errors \
    --prefix="$pkgdir/usr"
    # --use-system-v8
    # --use-sasl-client

  install -Dm644 $srcdir/mongodb.conf $pkgdir/etc/mongodb.conf
  install -Dm644 $srcdir/mongodb.service $pkgdir/usr/lib/systemd/system/mongodb.service
  install -dm700 $pkgdir/var/lib/mongodb
  install -dm755 $pkgdir/var/log/mongodb
}
