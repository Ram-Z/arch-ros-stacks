# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: jepaan <jepaan at hotmail dot com>

pkgname=assimp
pkgver=3.0.1270
pkgrel=1
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner"
arch=('i686' 'x86_64')
license=('BSD')
depends=('freeglut' 'zlib' 'gcc-libs')
makedepends=('cmake' 'boost')
url=('http://assimp.sourceforge.net/index.html')
source=("http://downloads.sourceforge.net/assimp/assimp--$pkgver-source-only.zip")
md5sums=('52aa4cf4e34e6b2a9c5f6c0b3c319af1')

build() {
  cd $pkgname--$pkgver-source-only

  mkdir -p build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DASSIMP_BUILD_SAMPLES=ON
  make
}

package()  {
  cd $pkgname--$pkgver-source-only/build

  make DESTDIR=$pkgdir install
  install -Dm644 $srcdir/$pkgname--$pkgver-source-only/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
