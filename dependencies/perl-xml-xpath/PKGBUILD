# Maintainer: Jakub Kozisek <nodevel at gmail dot com>

pkgname=perl-xml-xpath
pkgver=1.13
pkgrel=1
pkgdesc="A set of modules for parsing and evaluating XPath statements."
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl' 'perl-xml-parser>=2.23')
url=https://metacpan.org/release/XML-XPath
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSERGEANT/XML-XPath-${pkgver}.tar.gz")
md5sums=(b5919d9220d83982feb6e2321850c5d7)
sha512sums=(c5af746f36562803e42c5afb63b533b59ecaa263977c5ef4bf6488b7597cae334bb3b0c644acdfbb453e87a22ba9a785df3f81b95faec89d974704d6f04f6503)

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/XML-XPath-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/XML-XPath-${pkgver}"
    make test
)

package() {
    cd "${srcdir}/XML-XPath-${pkgver}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
