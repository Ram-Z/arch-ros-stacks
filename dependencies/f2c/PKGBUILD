# Maintainer: Samir Benmendil <ram-z[at]chakra-project[dot]org>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Jesin <jesin00@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Gabriele Lanaro <gabriele.lanaro@gmail.com>

pkgname=f2c
pkgver=1.0
pkgrel=1
pkgdesc='Fortran to C code translator'
url='http://www.netlib.org/f2c/index.html'
license=('custom')
arch=('x86_64' 'i686')
source=("http://computation.llnl.gov/casc/Overture/henshaw/software/${pkgname}.tar.gz")
sha256sums=('5cac8440ebfaf0958296044219935bfcf5d0bff318fe2e4f9ac0290007fb73c8')

build() {
  cd "${srcdir}/${pkgname}"

  make -C src CFLAGS="-O ${CFLAGS} -fPIC"
  make -C libf2c CFLAGS="-O ${CFLAGS} -fPIC"

  cd libf2c
  gcc ${CPPFLAGS} ${CFLAGS} ${LDFLAGS} \
    -shared -Wl,-soname,libf2c.so.2 -o libf2c.so.2 *.o -lc
}

package() {
  cd "${srcdir}/${pkgname}"

  install -Dm755 src/f2c "${pkgdir}/usr/bin/f2c"
  install -Dm644 libf2c/libf2c.a "${pkgdir}/usr/lib/libf2c.a"
  make -C libf2c LIBDIR="${pkgdir}/usr/lib" install
  install -Dm644 libf2c/f2c.h "${pkgdir}/usr/include/f2c.h"
  install -Dm755 libf2c/libf2c.so.2 "${pkgdir}/usr/lib/libf2c.so.2"
  install -Dm644 src/f2c.1t "${pkgdir}/usr/share/man/man1/f2c.1"
  install -Dm644 src/Notice "${pkgdir}/usr/share/licenses/f2c/LICENSE"
} 

# vim:set ts=2 sw=2 et:
