
pkgdesc="ROS - Base dependencies and support libraries for ROS."
url='http://www.ros.org/'

pkgname='ros-hydro-roslib'
pkgver='1.10.9'
_pkgver_patch=0
arch=('i686' 'x86_64')
pkgrel=3
license=('BSD')

ros_makedepends=(ros-hydro-rospack
  ros-hydro-catkin)
makedepends=('cmake' 'git' 'ros-build-tools'
  ${ros_makedepends[@]}
  boost)

ros_depends=(ros-hydro-rospack
  ros-hydro-catkin)
depends=(${ros_depends[@]}
  python2-rospkg)

_tag=release/hydro/roslib/${pkgver}-${_pkgver_patch}
_dir=roslib
source=("${_dir}"::"git+https://github.com/ros-gbp/ros-release.git"#tag=${_tag}
        "python.patch")
md5sums=('SKIP'
         '99fe5ee6ab3bc658a128367ae34fd7b4')

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Apply patch
  msg "Patching source code"
  cd ${srcdir}/${_dir}
  git apply ${srcdir}/python.patch

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python2/Python3 conflicts
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Build project
  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
